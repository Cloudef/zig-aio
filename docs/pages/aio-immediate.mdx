# AIO API

## Immediate IO

For immediate blocking IO, `zig-aio` provides the following functions in the `aio` module.

### Perform a single operation

Completes a single operation, the call blocks until it's complete.
Returns error of the operation if the operation failed.
Returns `void` if there was no error.

```zig
try aio.single(.write, .{ .file = f, .buffer = "contents" });
```

### Perform multiple operations

`zig-aio` provides two methods for batching IO operations.

#### Using multi

Completes a list of operations immediately, blocks until complete.
Returns `error.SomeOperationFailed` if any operation failed.
Returns `void` if there were no errors.

```zig
var my_buffer: [1024]u8 = undefined;
var my_len: usize = undefined;

try aio.multi(.{
    aio.op(.write, .{ .file = f, .buffer = "contents" }, .soft),
    aio.op(.read, .{ .file = f, .buffer = &my_buffer, .out_read = &my_len }, .unlinked),
});
```

The `.link` field of operation can be used to link the operation to the next operation.
When linking operations, the next operation won't start until this operation is complete.

`soft` link will propagate failure to next operations in the link chain.
`hard` link will not propagate failure, and the next operation starts normally.

#### Using complete

Complete is similar to multi, but it will not return `error.SomeOperationFailed` in case any of the operations fail.
Instead complete returns the number of errors occured. To find out which operations failed, errors have to be stored
somewhere by setting the `.out_error` field of the operation. The complete call may still fail in implementation
defined ways, such as running out of system resources.

```zig
var my_buffer: [1024]u8 = undefined;
var my_len: usize = undefined;
var write_error: aio.Write.Error = undefined;
var read_error: aio.Read.Error = undefined;

const res = try aio.complete(.{
    aio.op(.write, .{ .file = f, .buffer = "contents", .out_error = &write_error }, .soft),
    aio.op(.read, .{ .file = f, .buffer = &my_buffer, .out_read = &my_len, .out_error = &read_error }, .unlinked),
});

if (res.num_errors > 0) {
    if (write_error != error.Success) @panic("write failed");
    if (read_error != error.Success) @panic("read failed");
}
```
